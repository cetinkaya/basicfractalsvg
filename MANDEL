10 REM ***MANDELBROT SET, TOTAL VIEW***
20 REM ***NAME:MANDEL***
30 SCREEN 3 : CLS
40 WINDOW (-2.2,-1.4)-(1.1,1.4)
50 N1=160 : N2=100 : REM ***RESOLUTION***
60 FOR I=-N1 TO N1 : A=-.55+1.65*I/N1
65 PRINT I
70 FOR J=0 TO N2 : B=1.4*J/N2
80 U=4*(A*A+B*B) : V=U-2*A+1/4
90 IF U+8*A+15/4<0 THEN K=1 : GOTO 170
100 IF V-SQR(V)+2*A-1/2<0 THEN K=1 : GOTO 170
110 X=A : Y=B
120 FOR K=1 TO 50
130 U=X*X : V=Y*Y : W=2*X*Y
140 X=U-V+A : Y=W+B
150 IF U+V>16 THEN GOTO 170
160 NEXT K
170 L=K MOD 2 : PSET (A,B),L : PSET (A,-B),L
180 NEXT J : NEXT I
190 BEEP : A$=INPUT$(1)